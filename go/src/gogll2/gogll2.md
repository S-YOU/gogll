# Gogll2

# Scannerless Generalised LL Parsing
Gogll generates scannerless *generalised LL parsers*[1], specifically clustered nonterminal BSR parsers (CNP) [2].

# Walking the Parse Forest
Gogll produces a parse forest represented by a set of Binary Subtree Representations. See [BSR](bsr.md) for details of walking the parse forest.

# Input Symbols, Markdown Files
Gogll accepts UTF-8 input strings. 
A gogll parser has two parse functions: 
* `Parse(I []byte) []*ParseError`
* `func ParseFile(fname string) []*ParseError`   
If `fname` ends with `.md` the parser ignores all text outside the markdown code blocks delimited by triple backticks. 

This document contains BNF specification for gogll V2, which is is generated by gogll. 

`GoGLL` is the start symbol of the grammar of GoGLL2.
```
package "gogll2"

*GoGLL : Package Rules ;
```

```
Package : SepE "package" Sep String SepE ;
```

```
Rules
    :   Rule
    |   Rule SepE Rules
    ;
```

```
Rule : Head SepE ":" SepE Alternates ";" ;

Head : NonTerminal ;

Alternates
    :   Alternate
    |   Alternate SepE "|" SepE Alternates
    ;

Alternate
    :   Symbol
    |   Symbol Sep Alternate
    |   "empty"
    ;

Symbol : NonTerminal | Terminal ;

NonTerminal : letter NTChars ;

NTChars
    :   NTChar
    |   NTChar NTChars
    ;

NTChar : letter | number | anyof "!@#$%^&*-_+=" ;

Terminal
    :   "any"
    |   "anyof" Sep String
    |   "letter"
    |   "number"
    |   "space"
    |   "upcase"
    |   "lowcase"
    |   "not" String
    |   CharLiteral 
    |   String
    ;
```

* `any` accepts any character  
* `anyof String` accepts any character that is an element of `String`  
* `letter` accepts any character from the Unicode letter category  
* `number` accepts any character from the Unicode number category  
* `space` accepts any Unicode white space character
* `upcase` accepts any upper case letter
* `lowcase` accepts any lower case letter
* `not "String"` accepts any character that is not an element of `String`

```
CharLiteral 
    :   "\'" '\\' EscapedChar "\'"
    |   "\'" any "\'"
    ;
```

Examples:

    '\n'
    '\r'

```
EscapedChar : "\"" | "n" | "r" | "t" | "\\" | "\'" ;

String : "\"" StringChars "\"" ;

StringChars
    :   not "\""
    |   emptyAlt
    ;

SepE
    :   Sep
    |   emptyAlt
    ;

Sep
    :   SepChar
    |   SepChar Sep
    ;

SepChar : space | anyof "\t\n\t" ;
```

# References
1. Elizabeth Scott and Adrian Johnstone,
*GLL Parsing*,
Electronic Notes in Theoretical Computer Science 253 (2010) 177–189 

2. Elizabeth Scott, Adrian Johnstone and L. Thomas van Binsbergen,
*Derivation representation using binary subtree sets*,
Science of Computer Programming 175 (2019) 63–84
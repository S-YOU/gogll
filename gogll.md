# Gogll v2

[Copyright 2019 Marius Ackerman](License.txt)

This document contains BNF specification for gogll V1. This version is a gogll grammar and the parser for gogll v1 is generated by gogll v0.

# Lexical Definitions
```
_lowcase : 'a'-'z'; 
_upcase : 'A'-'Z';
_number : '0'-'9' ;
_id_char : _upcase | _lowcase | _number | '_' ;
nt : _upcase _id_char {_id_char} ;
tokid : _lowcase _id_char {_id_char} ;

_esc_char : '\\' ('\\' | '\'' | '"' | 'n' | 'r' | 't');
_string_char : _esc_char | . ;
string_lit : '"' _string_char {_string_char} '"' ;

!whitespace : ' ' | '\t' | '\n' | '\r' ;

```
char_literal : '\'' ('\\' _esc_char | .) '\'' ;


```
<<import "github.com/goccmack/gogll/ast">>

GoGLL : Package Rules << ast.NewGoGLL($0, $1) >>;

Package : "package" string_lit << ast.NewPackage($1) >>;

Rules
    :   Rule            << ast.NewRules($0) >>
    |   Rules Rule      << ast.AddRule($0, $1) >>
    ;

Rule : NT ":" Alternates ";" << ast.NewRule($0, $2) >> ;

NT : nt  << ast.NewNT($0) >>;

Alternates
    :   Alternate                   << ast.NewAlternates($0) >>
    |   Alternates "|" Alternate    << ast.AddAlternate($0, $2) >>
    ;

Alternate
    :   Symbols                     << ast.NewAlternate($0) >>
    |   "empty"                     << ast.NewAlternate($0) >>
    ;

Symbols
    :   Symbol                      << ast.NewSymbols($0) >>
    |   Symbols Symbol              << ast.AddSymbol($0, $1) >>
    ;

Symbol : NT | Terminal ;

Terminal
    :   tokid                       << ast.NewTokID($0) >>
    |   string_lit                  << ast.NewStringLit($0) >>
    ;
```

-   `any` accepts any character
-   `anyof String` accepts any character that is an element of `String`
-   `letter` accepts any character from the Unicode letter category
-   `number` accepts any character from the Unicode number category
-   `space` accepts any Unicode white space character
-   `upcase` accepts any upper case letter
-   `lowcase` accepts any lower case letter
-   `not "String"` accepts any character that is not an element of `String`


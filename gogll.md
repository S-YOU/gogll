# Gogll v1

This document contains BNF specification for gogll V1. This version is a gogll grammar and the parser for gogll v1 is generated by gogll v0. 

# Start symbol
`GoGLL` is the start symbol of the grammar of GoGLL2.
```
package "gogll"

*GoGLL : SepE Package Sep Rules SepE ;
```

```
Package : "package" Sep String ;
```

```
Rules
    :   Rule
    |   Rule SepE Rules
    ;
```

```
Rule : Head SepE ":" SepE Alternates SepE ";" ;

Head : StartSymbol | NonTerminal ;

StartSymbol : "*" SepE NonTerminal ;

Alternates
    :   Alternate
    |   Alternate SepE "|" SepE Alternates
    ;

Alternate
    :   Symbol
    |   Symbol Sep Alternate
    |   "empty"
    ;

Symbol : NonTerminal | Terminal ;

NonTerminal 
    : letter 
    | letter NTChars ;

NTChars
    :   NTChar
    |   NTChar NTChars
    ;

NTChar : letter | number | anyof "!@#$%^&*-_+=" ;

Terminal
    :   "any"
    |   "anyof" Sep String
    |   "letter"
    |   "number"
    |   "space"
    |   "upcase"
    |   "lowcase"
    |   "not" Sep String
    |   CharLiteral 
    |   String
    ;
```

* `any` accepts any character  
* `anyof String` accepts any character that is an element of `String`  
* `letter` accepts any character from the Unicode letter category  
* `number` accepts any character from the Unicode number category  
* `space` accepts any Unicode white space character
* `upcase` accepts any upper case letter
* `lowcase` accepts any lower case letter
* `not "String"` accepts any character that is not an element of `String`

```
CharLiteral 
    :   "\'" '\\' anyof "nrt\\'\"" "\'"
    |   "\'" any "\'"
    ;
```

Examples:

    '\n'
    '\r'

```
EscapedChar : '"' | "n" | "r" | "t" | "\\" | "\'" ;

String : "\"" StringChars "\"" ;

StringChars
    :   not "\\\"" StringChars
    |   "\\" EscapedChar StringChars
    |   emptyAlt
    ;

SepE
    :   Sep
    |   emptyAlt
    ;

Sep
    :   SepChar
    |   SepChar Sep
    ;

SepChar : space | anyof "\t\n\t" ;
```

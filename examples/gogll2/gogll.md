# Gogll v2

This document contains BNF specification for gogll V1. This version is a gogll grammar and the parser for gogll v1 is generated by gogll v0. 

```
package "gogll/examples/gogll2"

*GoGLL : SepE Package Sep Rules SepE ;

Package : "package" Sep String ;

Rules
    :   Rule
    |   Rule SepE Rules
    ;

Rule : Head SepE ":" SepE Alternates SepE ";" ;

Head : StartSymbol | NonTerminal ;

StartSymbol : "*" SepE NonTerminal ;

Alternates
    :   Alternate
    |   Alternate SepE "|" SepE Alternates
    ;

Alternate
    :   Symbol
    |   Symbol Sep Alternate
    |   "empty"
    ;

Symbol : NonTerminal | Terminal ;

NonTerminal : letter NTChars ;

NTChars
    :   NTChar
    |   NTChar NTChars
    ;

NTChar : letter | number | anyof "!@#$%^&*-_+=" ;

Terminal
    :   "any"
    |   "anyof" Sep String
    |   "letter"
    |   "number"
    |   "space"
    |   "upcase"
    |   "lowcase"
    |   "not" String
    |   CharLiteral 
    |   String
    ;
```

* `any` accepts any character  
* `anyof String` accepts any character that is an element of `String`  
* `letter` accepts any character from the Unicode letter category  
* `number` accepts any character from the Unicode number category  
* `space` accepts any Unicode white space character
* `upcase` accepts any upper case letter
* `lowcase` accepts any lower case letter
* `not "String"` accepts any character that is not an element of `String`

```
CharLiteral 
    :   "\'" '\\' EscapedChar "\'"
    |   "\'" any "\'"
    ;


EscapedChar : '"' | "n" | "r" | "t" | "\\" | "\'" ;

String : '"' StringChars '"' ;

StringChars
    :   not "\""
    |   empty
    ;

SepE
    :   Sep
    |   empty
    ;

Sep
    :   SepChar
    |   SepChar Sep
    ;

SepChar : space | anyof "\t\n\t" ;
```
